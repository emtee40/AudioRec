apply plugin: 'com.android.application'
apply plugin: 'com.github.axet.dxplugin'

android {
    compileSdkVersion 29
    buildToolsVersion '28.0.3'

    defaultConfig {
        applicationId "com.github.axet.audiorecorder"
        minSdkVersion 9
        targetSdkVersion 29
        versionCode        354
        versionName        "3.4.4"
    }
    signingConfigs {
        release {
            storeFile file(project.findProperty('RELEASE_STORE_FILE')?:'none')
            storePassword project.findProperty('RELEASE_STORE_PASSWORD')
            keyAlias project.findProperty('RELEASE_KEY_ALIAS')
            keyPassword project.findProperty('RELEASE_KEY_PASSWORD')
        }
    }
    buildTypes {
        debug {
            debuggable true
            jniDebuggable true
            minifyEnabled false
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFileName = org.apache.commons.io.FilenameUtils.getExtension(android.defaultConfig.applicationId) + "-" + (output.getFilter(com.android.build.OutputFile.ABI) ?: "universal") + "-" + android.defaultConfig.versionName + "-" + variant.buildType.name + ".apk"
        }
    }
    bundle {
        abi {
            enableSplit = true
        }
    }
    splits {
        abi {
            enable false
            universalApk true
        }
    }
}

dependencies {
    testImplementation 'junit:junit:4.12'
    implementation 'com.github.axet:android-library:1.33.6'
    implementation ('com.github.axet:android-audio-library:1.0.180') { exclude module: 'android-library' } // implementation project(':android-audio-library')
    implementation ('com.github.axet:wget:1.7.0') { exclude group: 'org.json', module: 'json' }
    assets('com.google.android.exoplayer:exoplayer:2.7.3') { exclude group: 'com.android.support' }
}
